def luckyConfigDir = "$projectDir/lucky_config"
def luckyRunConfigDir = "$projectDir/run/config/lucky_block/version-${project.version}"
def luckyAddonDir = "$projectDir/template_addon"
def luckyRunAddonDir = "$projectDir/run/addons/lucky_block/lucky_block_custom"
def distDir = "$buildDir/libs"

task luckyCopyRunConfig(type: Copy) {
    group = 'Lucky Block'
    description = 'Copies Lucky Block configuration to the run directory.'
    from luckyConfigDir
    into luckyRunConfigDir
}

task luckyCopyTemplateAddon(type: Copy) {
    group = 'Lucky Block'
    description = 'Copies Lucky Block template add-on to the run directory.'
    from luckyAddonDir
    into luckyRunAddonDir
}

task luckyZipConfig(type: Zip) {
    group = 'Lucky Block'
    description = 'Creates a Lucky Block configuration zip archive.'
    destinationDir = file(distDir)
    archiveName = 'default_config.zip'
    from luckyConfigDir
}

task luckyPackage(type: Zip) {
    group = 'Lucky Block'
    description = 'Combines build archive with Lucky Block configuration and generates the final zip.'
    destinationDir = file(distDir)
    archiveName = "${project.jarName}.zip" 
    from zipTree("$distDir/${project.jarNameRaw}.jar")
    from("$distDir/default_config.zip") {
        into ('mod/lucky')
    }
}

task luckyClient {
    group = 'Lucky Block'
    description = 'Runs Minecraft client with the Lucky Block.'
    dependsOn luckyCopyRunConfig
    dependsOn luckyCopyTemplateAddon
    dependsOn 'runClient'
}
task luckyServer {
    group = 'Lucky Block'
    description = 'Runs Minecraft server with the Lucky Block.'
    dependsOn luckyCopyRunConfig
    dependsOn luckyCopyTemplateAddon
    dependsOn 'runServer'
}

task luckyBuild {
    group = 'Lucky Block'
    description = 'Builds the Lucky Block.'
    dependsOn build
    dependsOn luckyZipConfig
    dependsOn luckyPackage

    luckyZipConfig.shouldRunAfter(build)
    luckyPackage.shouldRunAfter(luckyZipConfig)
}
