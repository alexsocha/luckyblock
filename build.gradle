version = '1.16.1-1'
ext {
    FORGE_VERSION = '1.16.1-32.0.46'
    MC_VERSION = '1.16.1'
    MAPPING_VERSION = '20200514-1.16'
    JAR_NAME_DEV = 'luckyblock-build'
    JAR_NAME_PRD = "luckyblock-$version"
}

buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
    }
}
apply plugin: 'net.minecraftforge.gradle'

group = 'mod.lucky'
archivesBaseName = 'lucky'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {
    mappings channel: 'snapshot', version: "$MAPPING_VERSION"
    
    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.console.level', 'info'
            mods { lucky { source sourceSets.main } }
        }
        server {
            workingDirectory project.file('run')
            property 'forge.logging.console.level', 'info'
            mods { lucky { source sourceSets.main } }
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${project.FORGE_VERSION}"
}

apply from: 'scripts/tasks.gradle' // custom tasks

jar {
    archiveName = "${project.JAR_NAME_DEV}.jar"
    manifest {
        attributes([
            'Specification-Title': 'luckyblock',
            'Specification-Vendor': 'alexsocha',
            'Specification-Version': '1',
            'Implementation-Title': 'luckyblock',
            'Implementation-Version': version,
            'Implementation-Vendor': 'alexsocha',
            'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

processResources {
    eachFile {
        if (it.relativePath.segments[0] in ['META-INF']) {
            expand 'version':version
        }
    }
}
